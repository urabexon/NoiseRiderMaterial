vec3 random3(vec3 c) {
	float j = 4096.0*sin(dot(c,vec3(17.0, 59.4, 15.0)));
	vec3 r;
	r.z = fract(512.0*j);
	j *= .125;
	r.x = fract(512.0*j);
	j *= .125;
	r.y = fract(512.0*j);
	return r-0.5;
}

/* skew constants for 3d simplex functions */
const float F3 =  0.3333333;
const float G3 =  0.1666667;

/* 3d simplex noise */
float simplex3d(vec3 p) {
	 /* 1. find current tetrahedron T and it's four vertices */
	 /* s, s+i1, s+i2, s+1.0 - absolute skewed (integer) coordinates of T vertices */
	 /* x, x1, x2, x3 - unskewed coordinates of p relative to each of T vertices*/
	 
	 /* calculate s and x */
	 vec3 s = floor(p + dot(p, vec3(F3)));
	 vec3 x = p - s + dot(s, vec3(G3));
	 
	 /* calculate i1 and i2 */
	 vec3 e = step(vec3(0.0), x - x.yzx);
	 vec3 i1 = e*(1.0 - e.zxy);
	 vec3 i2 = 1.0 - e.zxy*(1.0 - e);
	 	
	 /* x1, x2, x3 */
	 vec3 x1 = x - i1 + G3;
	 vec3 x2 = x - i2 + 2.0*G3;
	 vec3 x3 = x - 1.0 + 3.0*G3;
	 
	 /* 2. find four surflets and store them in d */
	 vec4 w, d;
	 
	 /* calculate surflet weights */
	 w.x = dot(x, x);
	 w.y = dot(x1, x1);
	 w.z = dot(x2, x2);
	 w.w = dot(x3, x3);
	 
	 /* w fades from 0.6 at the center of the surflet to 0.0 at the margin */
	 w = max(0.6 - w, 0.0);
	 
	 /* calculate surflet components */
	 d.x = dot(random3(s), x);
	 d.y = dot(random3(s + i1), x1);
	 d.z = dot(random3(s + i2), x2);
	 d.w = dot(random3(s + 1.0), x3);
	 
	 /* multiply d by w^4 */
	 w *= w;
	 w *= w;
	 d *= w;
	 
	 /* 3. return the sum of the four surflets */
	 return dot(d, vec4(52.0));
}

/* const matrices for 3d rotation */
const mat3 rot1 = mat3(-0.37, 0.36, 0.85,-0.14,-0.93, 0.34,0.92, 0.01,0.4);
const mat3 rot2 = mat3(-0.55,-0.39, 0.74, 0.33,-0.91,-0.24,0.77, 0.12,0.63);
const mat3 rot3 = mat3(-0.71, 0.52,-0.47,-0.08,-0.72,-0.68,-0.7,-0.45,0.56);

/* directional artifacts can be reduced by rotating each octave */
float simplex3d_fractal(vec3 m) {
    return   0.5333333*simplex3d(m*rot1)
			+0.2666667*simplex3d(2.0*m*rot2)
			+0.1333333*simplex3d(4.0*m*rot3)
			+0.0666667*simplex3d(8.0*m);
}





//////////////////////////////////
//
//  Noise Kokomade
//
//////////////////////////////////





vec3 originalPicture(vec2 uv,int depth)
{
    float interval = 1.0/64.0;
    bool lines = mod(uv.x+uv.y+interval*0.25*sqrt(2.), interval) < interval/3.0;
    bool circles = mod(length(uv)+interval/4.0, interval) < interval/2.0;
    return circles ? vec3(1.,0.6+0.2*mod(float(depth),3.)*0.,0. ) : vec3(0.,0.,0.);
}

vec2 foldByDepth(vec2 uv, int depth)
{
    vec2 result = uv;
    float freq = float(1 << depth);
    float size = 1.0 / freq;
    result.x = mod(result.x, size);
    result.x = result.x < size / 2.0 ? result.x : size - result.x;
    //result.x = result.x * freq;
    result.y = mod(result.y, size);
    result.y = result.y < size / 2.0 ? result.y : size - result.y;
    return result;
}

vec2 leftDownByDepth(vec2 uv, int depth)
{
    vec2 result = uv;

    float freq = float(1 << depth);
    float size = 1.0 / freq;

    result.x -= mod(result.x, size);
    result.y -= mod(result.y, size);
    
    return result;
}

vec2 rotate2d(vec2 v, float a)
{
    float c = cos(a);
    float s = sin(a);
    return vec2(v.x*c - v.y*s, v.x*s + v.y*c);
}

float signedPow(float x, float a) {return sign(x)*pow(abs(x),a);}



void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    vec4 col = vec4(0, 0, 0, 1);

    vec2 uv = fragCoord/iResolution.xy * pow(0.95 + 0.15*sin(iTime*0.13),4.0);
    uv.x *= (iResolution.x/iResolution.y);
    
    vec2 p = uv;
    vec2 center = vec2((iTime - cos(iTime*0.13)/0.13)*0.2,2.5);
    p += center;
    
    p = rotate2d(p-center,(iTime - cos(iTime*0.13))*0.06) + center;
    //p += 0.5;
    //p += 0.00*vec2(simplex3d(vec3(uv+iTime*0.03, iTime*0.21)), simplex3d(vec3(uv.yx+iTime*0.03, iTime*0.181+100.0)));
    
    

    vec2 toSee = p;

    int depth = 0;
    for (int j = 0; j < 6; j++)
    {
        vec2 uv = p;

        float jj = float(j);
        float r = simplex3d(vec3(leftDownByDepth(p, j)*10.0, 0.) + vec3(jj*10., jj*10., iTime/(jj+1.)))*0.5+0.5;
        if (r < pow(0.05*jj,0.35)+0.02)
        {
            break;
        }
        depth++;
    }

    toSee = foldByDepth(toSee, depth);
    col.xyz = originalPicture(toSee, depth);
	
	fragColor = col ;
	return;
}
